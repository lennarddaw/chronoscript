start: statement+

statement: "loop" "every" TIME "if" condition "{" action+ "}"

?condition: bool_expr

?bool_expr: bool_term
          | bool_expr "||" bool_term   -> or_expr

?bool_term: bool_factor
          | bool_term "&&" bool_factor -> and_expr

?bool_factor: "!" bool_factor          -> not_expr
            | func_call
            | simple_cond
            | "(" bool_expr ")"

simple_cond: CNAME
func_call: CNAME "(" [condition_args] ")"
condition_args: condition_arg ("," condition_arg)*
condition_arg: ESCAPED_STRING | NUMBER | CNAME
action: CNAME "(" string? ")" ";"

string: ESCAPED_STRING

TIME: /\d+[smhd]/

%import common.WS
%import common.CNAME
%import common.NUMBER
%import common.ESCAPED_STRING
%import common.WS
%ignore WS
